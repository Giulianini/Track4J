buildscript {
	repositories {
	    jcenter()
	    mavenCentral()
	}
	dependencies {
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'signing'
}

group = project_group
archivesBaseName = project_artifact
version = version

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
	classifier = 'all'
	manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

configurations {
    localCompile.transitive = false
    compile.extendsFrom(localCompile)
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.project_name
        desc = project.project_description
        licenses = [project.project_license_tag]
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/$project.your_github_name/$project.project_name"
        issueTrackerUrl = "https://github.com/$project.your_github_name/$project.project_name/issues"
        vcsUrl = "https://github.com/$your_github_name/${project.project_name}.git"
        githubRepo = "$your_github_name/$project.project_name"
        version {
            name = version //project.project_version
            vcsTag = version //project.project_version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project_group
            artifactId project_artifact
            version version //project_version
            artifact sourcesJar
            artifact javadocJar
            artifact fatJar
        }
    }
}

repositories {
    mavenCentral()
   	jcenter()
   	flatDir {
   	    dir 'libs'
   	}
}

dependencies {
	//compile all jar in libs
	localCompile fileTree(dir: 'libs', include: '*.jar')
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.google.guava:guava:23.0'
    testCompile 'junit:junit:4.12'
    compile 'com.github.haifengl:smile-core:1.5.1'
    compile 'com.jfoenix:jfoenix:8.0.3' // Java 8
    compile 'org.kordamp.ikonli:ikonli-material-pack:2.1.1'
    compile 'org.kordamp.ikonli:ikonli-javafx:2.1.1'
    compile 'eu.hansolo.enzo:Enzo:0.3.6'
    compile 'com.google.code.gson:gson:2.8.4' 
}

sourceSets {
    main {
        java {
            srcDirs= ["src/main/java"]
        }
        resources {
            srcDirs= ["src/main/resources"]
        }
        
    }
}

jar {
    doFirst {
        manifest {
            if (!configurations.compile.isEmpty()) {
                attributes(
                        'Class-Path': configurations.localCompile.collect{it.getName()}.join(' '))
            }
        }
    }
    from {
        configurations.localCompile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

signing {
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

if (!project.hasProperty('ossrhUsername')) ext.ossrhUsername = ''
if (!project.hasProperty('ossrhPassword')) ext.ossrhPassword = ''

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name project_name
        packaging 'jar'
        // optionally artifactId can be defined here 
        description project_description
        url "https://github.com/$project.your_github_name/$project.project_name"

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'giulianini'
            name 'Luca Giulianini'
            email 'luca.giulianini2@studio.unibo.it'
          }
        }
      }
    }
  }
}

